<!DOCTYPE HTML>
<html>
<head>
    <link rel="stylesheet" type="text/css" href="../Styles/Samples.css"/>
    <script src="../Scripts/webglhelper.js" type="text/javascript"></script>
    <script src="../Scripts/EventHandling.js" type="text/javascript"></script>
    <script src="../Scripts/Common.js" type="text/javascript"></script>
    <script src="../Scripts/FPSCounter.js" type="text/javascript"></script>
    <script src="../Scripts/sylvester.js" type="text/javascript"></script> 
    <script src="../Scripts/glUtils.js" type="text/javascript"></script>
    <script src="../Scripts/GeometryUtils.js" type="text/javascript"></script>
    <script src="../Scripts/MatrixUtils.js" type="text/javascript"></script>
    <script src="A32FShadowMapping.js" type="text/javascript"></script>
    <script id="depth-pass-vs" type="x-shader/x-vertex">
        precision highp float;
        
        attribute vec3 aVertexPosition;
        
        varying vec2 vZW;
        
        uniform mat4 uWLPMatrix;
        
        void main(void) {
            gl_Position = uWLPMatrix * vec4(aVertexPosition, 1.0);
            vZW = gl_Position.zw;
        }
    </script>
    <script id="depth-pass-fs" type="x-shader/x-fragment">
        precision highp float;
        
        varying vec2 vZW;
        
        void main(void) {
            float result = vZW.x / vZW.y;
            gl_FragColor = vec4(result, result, result, result);
        }
    </script>
    <script id="main-pass-vs" type="x-shader/x-vertex">
        precision highp float;
        
        attribute vec3 aVertexPosition;
        attribute vec3 aVertexNormal;
        
        varying float vDiffuse;
        varying vec4 vLightSpasePos;
        
        uniform mat4 uWMatrix;
        uniform mat4 uWVPMatrix;
        uniform mat4 uWLPMatrix;
        uniform vec3 uLightDir;
        
        void main(void) {
            gl_Position = uWVPMatrix * vec4(aVertexPosition, 1.0);
            vec3 vNormal = normalize(mat3(uWMatrix) * aVertexNormal);
            vec3 vLightDir = normalize(uLightDir);
            vDiffuse = clamp(dot(vNormal, vLightDir), 0.0, 1.0) * 0.5 + 0.3;
            vLightSpasePos = uWLPMatrix * vec4(aVertexPosition, 1.0);
        }
    </script>
    <script id="main-pass-fs" type="x-shader/x-fragment">
        precision highp float;
        
        varying float vDiffuse;
        varying vec4 vLightSpasePos;
        
        uniform vec2 uTextureSize;
        uniform sampler2D uSampler;
        
        float CalcShadow(vec4 lpsPos) {

            vec2 shadowTexelSize = vec2(1.0, 1.0) / uTextureSize;
            vec3 projCoords = lpsPos.xyz / lpsPos.w;

	        vec4 shadowDepth;
	        shadowDepth.x = texture2D(uSampler, projCoords.xy).a > projCoords.z ? 1.0 : 0.0;
	        shadowDepth.y = texture2D(uSampler, projCoords.xy + vec2(shadowTexelSize.x,0)).a > projCoords.z ? 1.0 : 0.0;
	        shadowDepth.z = texture2D(uSampler, projCoords.xy + vec2(0,shadowTexelSize.y)).a > projCoords.z ? 1.0 : 0.0;
	        shadowDepth.w = texture2D(uSampler, projCoords.xy + vec2(shadowTexelSize.x,shadowTexelSize.y)).a > projCoords.z ? 1.0 : 0.0;

            vec2 lerps = fract(projCoords.xy * uTextureSize);
	        vec2 firstLerp = mix(shadowDepth.xz, shadowDepth.yw, lerps.x);
	        float shadow = mix(firstLerp.x, firstLerp.y, lerps.y);

	        return shadow;
        }

        void main(void) {
            float outColor = vDiffuse * (CalcShadow(vLightSpasePos) * 0.5 + 0.5);
            gl_FragColor = vec4(outColor, outColor, outColor, 1.0);
        }
    </script>
</head>
<body>
    <div id="WebGLCanvasContainer">
        <div id="FPSCounter"></div>
        <script id="WebGLCanvasCreationScript" type="text/javascript">
            WebGLHelper.CreateGLCanvasInline('WebGLCanvas', OnCanvasCreated, OnCanvasFailed)
        </script>
    </div>
</body>
</html>
